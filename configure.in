AC_PREREQ([2.66])
AC_INIT([NetNuke], [2.0.2], [jhunkeler@gmail.com], [netnuke], [https://github.com/jhunkeler/NetNuke])
AM_INIT_AUTOMAKE(1.11.1) 

AC_CONFIG_SRCDIR([config.h.in])
AC_CONFIG_HEADERS([config.h])

# Check for programs.
AC_PROG_AWK
AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

# Initializing libtool.
AC_DISABLE_SHARED
AC_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)
LIBTOOL="$LIBTOOL --silent"

# Checks for libraries.
AC_CHECK_LIB([pthread], [pthread_create], [],
	[AC_MSG_ERROR([required library pthread missing])])
	
AC_CHECK_LIB([ncurses], [initscr], [],
    [AC_MSG_ERROR([required library ncurses missing])])
    
AC_CHECK_LIB([ssl], [SSL_library_init], [],
    [AC_MSG_ERROR([required library openssl missing])])

# Checks for header files.
AC_CHECK_HEADERS_ONCE([errno.h fcntl.h linux/fs.h ncurses.h openssl/ssl.h stddef.h stdlib.h string.h sys/ioctl.h sys/syslog.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_INLINE
AC_TYPE_SIZE_T
AC_TYPE_SSIZE_T
AC_SYS_LARGEFILE

AC_CHECK_SIZEOF(unsigned long long)
AC_CHECK_SIZEOF(unsigned long int)

if test $ac_cv_sizeof_unsigned_long_long -ne 8; then
	AC_MSG_ERROR([unsigned long long is less than 8 bytes])
fi

if test $ac_cv_sizeof_unsigned_long_int -lt 4; then
	AC_MSG_ERROR([unsigned long int is less than 4 bytes])
fi

# Checks for library functions.
AC_FUNC_LSTAT_FOLLOWS_SLASHED_SYMLINK
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_CHECK_FUNCS_ONCE([getpagesize memmove memset strchr strerror strncasecmp strrchr strstr usleep])

AC_CONFIG_FILES([Makefile libsysfs/Makefile src/Makefile])

AC_OUTPUT
